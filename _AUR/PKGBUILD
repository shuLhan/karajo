# Maintainer: shulhan <ms@kilabit.info>

pkgname=karajo-git
pkgver=0.7.0.r42.gef379d6
pkgrel=1

pkgdesc="Worker, scheduler, and WebHook with HTTP."
arch=(x86_64)
url='https://sr.ht/~shulhan/karajo'
license=('GPL-3.0-or-later')

makedepends=(
	'go'
	'git'
)

provides=('karajo')

source=(
	"$pkgname::git+https://git.sr.ht/~shulhan/karajo"
)
sha256sums=(
	'SKIP'
)

backup=(
	'etc/karajo/karajo.conf'
	'srv/karajo/index.html'
)

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$pkgname"
	make build
}

package() {
	cd "$pkgname"
	make PREFIX=${pkgdir} install
}
